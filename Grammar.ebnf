program
	= { statement };

statement
	= ( [ simple_statement ], ";" )
	| block
	| if_statement
	| while_statement
	| repeat_statement
	| for_statement;

simple_statement
	= declaration
	| assignment
	| expression
	| break_statement
	| continue_statement;

while_statement
	= "while", "(", expression, ")", statement;

repeat_statement
	= "repeat", "(", expression, ")", statement;

for_statement
	= "for", "(", identifier, identifier, "in", expression, ")", statement;

break_statement
	= "break";

continue_statement
	= "continue";

if_statement
	= "if", "(", expression, ")", statement, [ "else", statement ];

block
	= "{", { statement }, "}";

declaration
	= identifier, identifier, [ invocation ];

assignment
	= identifier, ":", expression;

expression
	= comparison;

comparison
	= additive, { ( "<"
	| ">"
	| "<="
	| ">="
	| "=" ), additive };

additive
	= multiplicative, { ( "+"
	| "-" ), multiplicative };

multiplicative
	= unary, { ( "*"
	| "/" ), unary };

unary
	= [ ( "+"
	| "-" ) ], primary;

primary
	= number
	| boolean
	| string
	| identifier, [ invocation ]
	| "(", expression, ")";

invocation
	= "(", [ arguments ], ")";

arguments
	= expression, { ",", expression };

identifier
	= letter, { letter
	| digit };

string
	= '"', { character - '"' }, '"';

letter
	= "a"
	| "b"
	| "c"
	| "d"
	| "e"
	| "f"
	| "g"
	| "h"
	| "i"
	| "j"
	| "k"
	| "l"
	| "m"
	| "n"
	| "o"
	| "p"
	| "q"
	| "r"
	| "s"
	| "t"
	| "u"
	| "v"
	| "w"
	| "x"
	| "y"
	| "z"
	| "A"
	| "B"
	| "C"
	| "D"
	| "E"
	| "F"
	| "G"
	| "H"
	| "I"
	| "J"
	| "K"
	| "L"
	| "M"
	| "N"
	| "O"
	| "P"
	| "Q"
	| "R"
	| "S"
	| "T"
	| "U"
	| "V"
	| "W"
	| "X"
	| "Y"
	| "Z"
	| "_";

boolean
	= "true"
	| "false";

number
	= integer, [ ".", integer ];

integer
	= digit, { digit };

digit
	= "0"
	| "1"
	| "2"
	| "3"
	| "4"
	| "5"
	| "6"
	| "7"
	| "8"
	| "9";

character
	= ? any valid source character ?;